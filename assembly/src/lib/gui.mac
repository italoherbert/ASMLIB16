%ifndef GUI_MAC
	%define GUI_MAC
			
	%include "lib/system.mac"
	%include "lib/math.mac"
	%include "lib/graph.mac"

	%include "src/lib/constants.asm"
	
section .data	
	__show_init_message: dw 0
	__show_win_message: dw 0
	__show_lose_message: dw 0
	__show_pause_message: dw 0

%macro gui_paint 0
	gui_paintbg
	gui_painttray
	gui_paintball
	gui_paintracket
	repaint
	
	cmp16 __show_init_message, __ZERO, cl
	cmp cl,0
	je %%init_message_painted
		gui_show_message 'Pressione alguma tecla..', 8, 13, __message_default_color		
	%%init_message_painted:
		
	cmp16 __show_pause_message, __ZERO, cl
	cmp cl,0
	je %%pause_message_painted		
		gui_show_message 'Pausa...', 16, 14, __message_default_color
	%%pause_message_painted:	
		
	cmp16 __show_win_message, __ZERO, cl
	cmp cl,0
	je %%win_message_painted
		gui_show_message 'Ganhou...', 12, 13, __message_win_color
		gui_show_message 'Parabens !!!', 12, 14, __message_default_color
	%%win_message_painted:
						
	cmp16 __show_lose_message, __ZERO, cl
	cmp cl,0
	je %%lose_message_painted	
		gui_show_message 'Perdeu...', 11, 13, __message_lose_color
		gui_show_message 'Tente novamente !', 11, 14, __message_default_color
	%%lose_message_painted:					
%endmacro

%macro gui_paintbg 0
	mov ax, 0
	mov bx, 0
	mov cx, 320
	mov dx, 200
	__fillrect ax, bx, cx, dx, 0 	
%endmacro

%macro gui_show_message 4
	section .data
	%%str: db %1,0
section .text
	mov ax,cs
	mov ds,ax
	mov al,[%4]
	__drawstring %%str, %2, %3, al
%endmacro
	
%macro gui_painttray 0
section .data
	%%cindex: dw 0
	%%def_cindex: dw 0
section .text
	mov16 %%cindex, %%def_cindex
	
	fillrect __tray_x1, __tray_y1, __tray_x2, __tray_y2, __tray_color
	drawlineh __tray_x1, __tray_y1, __tray_x2, __tray_bordercolor			
	drawlinev __tray_x1, __tray_y1, __tray_y2, __tray_bordercolor			
	drawlinev __tray_x2, __tray_y1, __tray_y2, __tray_bordercolor			
	
	mov16 __square_y, __tray_y1
	mov16 __square_y2, __square_y	
	add16 __square_y2, __square_h		
	
	mov cl,0
	%%l1:
		push cx
		int16toreg __square_ycount, dx
		pop cx
		cmp cl,dl
		jae %%l2
		push cx
		
		mov16 __square_x, __tray_x1		
		mov16 __square_x2, __square_x
		add16 __square_x2, __square_w
		
		mov cl,0 
		%%l1.1:
			push cx
			int16toreg __square_xcount, dx
			pop cx
			cmp cl,dl
			jae %%l1.2									
			push cx
						
			int16toreg __square_xcount, ax
			pop cx
			pop bx
			push bx
			push cx
						
			mul bx
			add ax,cx
												
			vlodsb __squaremap, ax, cl
			cmp cl,0
			jz %%painted
				int16toreg %%cindex, bx
				fillrect __square_x, __square_y, __square_x2, __square_y2, __square_ycolor+bx
				drawrect __square_x, __square_y, __square_x2, __square_y2, __square_bordercolor
			%%painted:													

			add16 __square_x, __square_w
			add16 __square_x2, __square_w				
		
			pop cx
			inc cl
			jmp %%l1.1
		%%l1.2:	
		
		add16 __square_y, __square_h		
		add16 __square_y2, __square_h				
		inc16 %%cindex		
													
		pop cx
		inc cl
		jmp %%l1	
	%%l2:
%endmacro		

%macro gui_paintball 0
section .data
	%%x: dw 0
	%%y: dw 0
	%%r: dw 0
section .text
	fltoint16 __ball_x, %%x
	fltoint16 __ball_y, %%y
	fltoint16 __ball_radial, %%r
	fillarc %%x, %%y, %%r, %%r, __ball_color
	drawarc %%x, %%y, %%r, %%r, __ball_bordercolor
%endmacro

%macro gui_paintracket 0
section .data
	%%x2: dw 0
	%%y2: dw 0
section .text
	mov16 %%x2, __racket_x
	mov16 %%y2, __racket_y
	add16 %%x2, __racket_w
	add16 %%y2, __racket_h
	fillrect __racket_x, __racket_y, %%x2, %%y2, __racket_color
	drawrect __racket_x, __racket_y, %%x2, %%y2, __racket_bordercolor
%endmacro				
	
%endif
